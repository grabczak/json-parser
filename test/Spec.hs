module Main (main) where

import Lib (JValue (..), fromJSON, toJSON)
import Test.Framework (Test, defaultMain, testGroup)
import Test.Framework.Providers.HUnit (testCase)
import Test.Framework.Providers.QuickCheck2 (testProperty)
import Test.HUnit (Assertion, assertEqual, assertFailure)
import Test.QuickCheck (
  Arbitrary (arbitrary),
  Gen,
  Property,
  Testable (property),
  elements,
  listOf,
  oneof,
  resize,
  sized,
  (===),
 )
import Text.Megaparsec (errorBundlePretty)

assertParseSuccess :: String -> JValue -> Assertion
assertParseSuccess input expected =
  case fromJSON input of
    Left err -> assertFailure $ "\n" ++ errorBundlePretty err
    Right result -> assertEqual ("Parsing:  " ++ input) expected result

assertParseFailure :: String -> Assertion
assertParseFailure input =
  case fromJSON input of
    Left _ -> return ()
    Right result -> assertFailure $ "Expected parse error. Instead got: " ++ show result

testNull :: Assertion
testNull = assertParseSuccess "null" JNull

testTrue :: Assertion
testTrue = assertParseSuccess "true" (JBool True)

testFalse :: Assertion
testFalse = assertParseSuccess "false" (JBool False)

testNumber :: Assertion
testNumber = assertParseSuccess "123.45" (JDouble 123.45)

testString :: Assertion
testString = assertParseSuccess "\"hello\"" (JString "hello")

testArray :: Assertion
testArray = assertParseSuccess "[null,true,1]" (JArray [JNull, JBool True, JInt 1])

testObject :: Assertion
testObject = assertParseSuccess "{\"a\":null,\"b\":false}" (JObject [("a", JNull), ("b", JBool False)])

testNegativeNumber :: Assertion
testNegativeNumber = assertParseSuccess "-21" (JInt (-21))

testEmptyString :: Assertion
testEmptyString = assertParseSuccess "\"\"" (JString "")

testCorrectFormat :: Assertion
testCorrectFormat =
  assertParseSuccess
    "{\"JSON Test Pattern pass3\":{\"The outermost value\":\"must be an object or array.\",\"In this test\":\"It is an object.\"}}"
    ( JObject
        [
          ( "JSON Test Pattern pass3"
          , JObject
              [ ("The outermost value", JString "must be an object or array.")
              , ("In this test", JString "It is an object.")
              ]
          )
        ]
    )

testDeepNesting :: Assertion
testDeepNesting = assertParseSuccess "[[[[[\"Deep nesting\"]]]]]" (JArray [JArray [JArray [JArray [JArray [JString "Deep nesting"]]]]])

testGeneralCase :: Assertion
testGeneralCase =
  assertParseSuccess
    "[\"JSON Test Pattern pass1\",{\"object with 1 member\":[\"array with 1 element\"]},{},[],-42,true,false,null,{\"integer\":1234567890,\"real\":-9876.54321,\"e\":1.23456789e-13,\"E\":1.23456789e+34,\"\":2.3456789012e+76,\"zero\":0,\"one\":1,\"space\":\" \",\"quote\":\"\\\"\",\"backslash\":\"\\\\\",\"controls\":\"\\b\\f\\n\\r\\t\",\"slash\":\"/ & /\",\"alpha\":\"abcdefghijklmnopqrstuvwyz\",\"ALPHA\":\"ABCDEFGHIJKLMNOPQRSTUVWYZ\",\"digit\":\"0123456789\",\"0123456789\":\"digit\",\"special\":\"`1~!@#$%^&*()_+-={':[,]}|;.</>?\",\"hex\":\"ģ䕧覫췯屴屴\",\"true\":true,\"false\":false,\"null\":null,\"array\":[],\"object\":{},\"address\":\"50 St. James Street\",\"url\":\"http://www.JSON.org/\",\"comment\":\"// /* <!-- --\",\"# -- --> */\":\" \",\" s p a c e d \":[1,2,3,4,5,6,7],\"compact\":[1,2,3,4,5,6,7],\"jsontext\":\"{\\\"object with 1 member\\\":[\\\"array with 1 element\\\"]}\",\"quotes\":\"&#34; \\\" %22 0x22 034 &#x22;\",\"/\\\\\\\"쫾몾ꮘﳞ볚屴\\b\\f\\n\\r\\t`1~!@#$%^&*()_+-=[]{}|;:',./<>?\":\"A key can be any string\"},0.5,98.6,99.44,1066,10,1,0.1,1,2,2,\"rosebud\"]"
    ( JArray
        [ JString "JSON Test Pattern pass1"
        , JObject [("object with 1 member", JArray [JString "array with 1 element"])]
        , JObject []
        , JArray []
        , JInt (-42)
        , JBool True
        , JBool False
        , JNull
        , JObject
            [ ("integer", JInt 1234567890)
            , ("real", JDouble (-9876.54321))
            , ("e", JDouble (1.23456789e-13))
            , ("E", JDouble (1.23456789e+34))
            , ("", JDouble (2.3456789012e+76))
            , ("zero", JInt 0)
            , ("one", JInt 1)
            , ("space", JString " ")
            , ("quote", JString "\"")
            , ("backslash", JString "\\")
            , ("controls", JString "\b\f\n\r\t")
            , ("slash", JString "/ & /")
            , ("alpha", JString "abcdefghijklmnopqrstuvwyz")
            , ("ALPHA", JString "ABCDEFGHIJKLMNOPQRSTUVWYZ")
            , ("digit", JString "0123456789")
            , ("0123456789", JString "digit")
            , ("special", JString "`1~!@#$%^&*()_+-={':[,]}|;.</>?")
            , ("hex", JString "ģ䕧覫췯屴屴")
            , ("true", JBool True)
            , ("false", JBool False)
            , ("null", JNull)
            , ("array", JArray [])
            , ("object", JObject [])
            , ("address", JString "50 St. James Street")
            , ("url", JString "http://www.JSON.org/")
            , ("comment", JString "// /* <!-- --")
            , ("# -- --> */", JString " ")
            , (" s p a c e d ", JArray (map JInt [1, 2, 3, 4, 5, 6, 7]))
            , ("compact", JArray (map JInt [1, 2, 3, 4, 5, 6, 7]))
            , ("jsontext", JString "{\"object with 1 member\":[\"array with 1 element\"]}")
            , ("quotes", JString "&#34; \" %22 0x22 034 &#x22;")
            , ("/\\\"쫾몾ꮘﳞ볚屴\b\f\n\r\t`1~!@#$%^&*()_+-=[]{}|;:',./<>?", JString "A key can be any string")
            ]
        , JDouble 0.5
        , JDouble 98.6
        , JDouble 99.44
        , JInt 1066
        , JInt 10
        , JInt 1
        , JDouble 0.1
        , JInt 1
        , JInt 2
        , JInt 2
        , JString "rosebud"
        ]
    )

passedParseTests :: [Test]
passedParseTests =
  [ testCase "Null" testNull
  , testCase "True" testTrue
  , testCase "False" testFalse
  , testCase "Number" testNumber
  , testCase "String" testString
  , testCase "Array" testArray
  , testCase "Object" testObject
  , testCase "Negative number" testNegativeNumber
  , testCase "Empty string" testEmptyString
  , testCase "Correct format" testCorrectFormat
  , testCase "Deep nesting" testDeepNesting
  , testCase "General case" testGeneralCase
  ]

testMisspelledNull :: Assertion
testMisspelledNull = assertParseFailure "nulls"

testExplicitString :: Assertion
testExplicitString = assertParseFailure "Explicit string"

testUnclosedArray :: Assertion
testUnclosedArray = assertParseFailure "[\"Unclosed array\""

testUnquotedKey :: Assertion
testUnquotedKey = assertParseFailure "{unquotedKey:true}"

testTrailingCommaInArray :: Assertion
testTrailingCommaInArray = assertParseFailure "[\"Trailing comma\",]"

testDoubleCommaInArray :: Assertion
testDoubleCommaInArray = assertParseFailure "[\"Double comma\",,]"

testMissingValueInArray :: Assertion
testMissingValueInArray = assertParseFailure "[,\"Missing value\"]"

testCommaAfterClosedArray :: Assertion
testCommaAfterClosedArray = assertParseFailure "[\"Comma after closing bracket\"],"

testExtraBracket :: Assertion
testExtraBracket = assertParseFailure "[\"Extra bracket\"]]"

testTrailingCommaInObject :: Assertion
testTrailingCommaInObject = assertParseFailure "{\"Trailing comma\":true,}"

testExtraValueAfterObject :: Assertion
testExtraValueAfterObject = assertParseFailure "{\"key\":\"value\"}\"Extra value after object\""

testIllegalExpression :: Assertion
testIllegalExpression = assertParseFailure "{\"Illegal expression\":1+2}"

testIllegalInvocation :: Assertion
testIllegalInvocation = assertParseFailure "{\"Illegal invocation\":alert()}"

testHexNumber :: Assertion
testHexNumber = assertParseFailure "{\"Hex number\":0x1A}"

testIllegalEscape :: Assertion
testIllegalEscape = assertParseFailure "{\"Illegal escape\":\"\\x\"}"

testEscapeOutsideString :: Assertion
testEscapeOutsideString = assertParseFailure "[\n]"

testIncorrectNesting :: Assertion
testIncorrectNesting = assertParseFailure "[[[[[[[[[[[[[[[[[[[[[]]]]]]]]]]]]]]]]]]]"

testMissingColon :: Assertion
testMissingColon = assertParseFailure "{\"key\"\"Missing colon\"}"

testDoubleColon :: Assertion
testDoubleColon = assertParseFailure "{\"key\"::\"Double colon\"}"

testCommaInsteadOfColonInObject :: Assertion
testCommaInsteadOfColonInObject = assertParseFailure "{\"key\",\"Comma instead of colon\"}"

testColonInsteadOfCommaInArray :: Assertion
testColonInsteadOfCommaInArray = assertParseFailure "[\"Colon instead of comma\":false]"

testIncorrectValue :: Assertion
testIncorrectValue = assertParseFailure "[\"Incorrect value\",truth]"

testSingleQuote :: Assertion
testSingleQuote = assertParseFailure "['single quote']"

testIncorrectExponent :: Assertion
testIncorrectExponent = assertParseFailure "[\"Incorrect exponent\",0e]"

testIncorrectPlusInExponent :: Assertion
testIncorrectPlusInExponent = assertParseFailure "[\"Incorrect plus in exponent\",0e+]"

testIncorrectMinusInExponent :: Assertion
testIncorrectMinusInExponent = assertParseFailure "[\"Incorrect minus in exponent\",0e+-1]"

testCommaInsteadOfClosingBrace :: Assertion
testCommaInsteadOfClosingBrace = assertParseFailure "{\"key\":\"value\","

testMismatch :: Assertion
testMismatch = assertParseFailure "[\"Mismatch\"}"

failedParseTests :: [Test]
failedParseTests =
  [ testCase "Misspelled null" testMisspelledNull
  , testCase "Explicit string" testExplicitString
  , testCase "Unclosed array" testUnclosedArray
  , testCase "Unquoted key" testUnquotedKey
  , testCase "Trailing comma in array" testTrailingCommaInArray
  , testCase "Double comma in array" testDoubleCommaInArray
  , testCase "Missing value in array" testMissingValueInArray
  , testCase "Comma after closed array" testCommaAfterClosedArray
  , testCase "Extra bracket" testExtraBracket
  , testCase "Trailing comma in object" testTrailingCommaInObject
  , testCase "Extra value after object" testExtraValueAfterObject
  , testCase "Illegal expression" testIllegalExpression
  , testCase "Illegal invocation" testIllegalInvocation
  , testCase "Hex number" testHexNumber
  , testCase "Illegal escape" testIllegalEscape
  , testCase "Escape outside string" testEscapeOutsideString
  , testCase "Incorrect nesting" testIncorrectNesting
  , testCase "Missing colon" testMissingColon
  , testCase "Double colon" testDoubleColon
  , testCase "Comma instead of colon in object" testCommaInsteadOfColonInObject
  , testCase "Colon instead of comma in array" testColonInsteadOfCommaInArray
  , testCase "Incorrect value" testIncorrectValue
  , testCase "Single quote" testSingleQuote
  , testCase "Incorrect exponent" testIncorrectExponent
  , testCase "Incorrect plus in exponent" testIncorrectPlusInExponent
  , testCase "Incorrect minus in exponent" testIncorrectMinusInExponent
  , testCase "Comma instead of closing brace" testCommaInsteadOfClosingBrace
  , testCase "Mismatch" testMismatch
  ]

newtype ArbJValue = ArbJValue {unArbJValue :: JValue}
  deriving (Show, Eq)

instance Arbitrary ArbJValue where
  arbitrary = ArbJValue <$> sized arbJValue

arbJValue :: Int -> Gen JValue
arbJValue n
  | n <= 0 = oneof [pure JNull, JBool <$> arbitrary, JInt <$> arbitrary, JDouble <$> arbitrary, JString <$> arbitraryString]
  | otherwise =
      oneof
        [ pure JNull
        , JBool <$> arbitrary
        , JInt <$> arbitrary
        , JDouble <$> arbitrary
        , JString <$> arbitraryString
        , JArray <$> resize (n `div` 2) (listOf (arbJValue (n `div` 2)))
        , JObject <$> resize (n `div` 2) (listOf ((,) <$> arbitraryString <*> arbJValue (n `div` 2)))
        ]

arbitraryString :: Gen String
arbitraryString = listOf $ elements (['a' .. 'z'] ++ ['A' .. 'Z'] ++ ['0' .. '9'] ++ " -_")

propertyIdentity :: ArbJValue -> Property
propertyIdentity (ArbJValue v) =
  let s = toJSON v
   in case fromJSON s of
        Right v' -> v' === v
        Left _ -> property False

qcTests :: [Test]
qcTests = [testProperty "Randomized identity test" propertyIdentity]

tests :: [Test]
tests =
  [ testGroup "Parse Success" passedParseTests
  , testGroup "Parse Failure" failedParseTests
  , testGroup "Identity" qcTests
  ]

main :: IO ()
main = defaultMain tests
